// include this file in subprojects:
// apply from: rootProject.file('libs/publish.gradle')

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

if( !hasProperty( 'bintrayUser' ) )
  ext.bintrayUser = ''

if( !hasProperty( 'bintrayKey' ) )
  ext.bintrayKey = ''

def thisProject = project

install.repositories.mavenInstaller.pom*.whenConfigured { pom ->
  pom.project {
    name thisProject.name
    packaging 'jar'
    description thisProject.project_description
    url thisProject.project_website

    scm {
      url thisProject.project_scm
      connection thisProject.project_scm
      developerConnection thisProject.project_scm
    }

    licenses {
      license {
        name thisProject.license
        url thisProject.license_url
        distribution 'repo'
      }
    }

    developers {
      developer {
        id thisProject.developer_id
        name thisProject.developer_name
      }
    }
  }
}

bintray {
  user = project.bintrayUser
  key = project.bintrayKey
  configurations = ['archives']
  pkg {
    repo = 'maven'
    name = thisProject.rootProject.project_id
    desc = thisProject.rootProject.description
    licenses = ['MIT']
    labels = thisProject.rootProject.labels
  }
  dryRun = false
}

bintrayUpload.dependsOn assemble
bintrayUpload.finalizedBy parent.tasks.bintraySign
